plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.7.10'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.7.10'
}

group = 'app.shamilton'

repositories {
    mavenCentral()
}

project.ext {
    versionFileLines = new File("src/nativeMain/kotlin/Version.kt").readLines()
    versionLine = versionFileLines.get(4)
    debianLine = versionFileLines.get(6)

    appVersion = versionLine.substring(1, versionLine.length()-1)
    debianVersion = debianLine.substring(1, debianLine.length()-1)
}

kotlin {
    def hostOs = System.getProperty("os.name")
    def isMingwX64 = hostOs.startsWith("Windows")
    def nativeTarget
    if (hostOs == "Mac OS X") nativeTarget = macosX64('native')
    else if (hostOs == "Linux") nativeTarget = linuxX64("native")
    else if (isMingwX64) nativeTarget = mingwX64("native")
    else throw new GradleException("Host OS is not supported in Kotlin/Native.")

    nativeTarget.with {
        binaries {
            executable {
                entryPoint = 'main'
            }
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-datetime:0.4.0"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.0"
                implementation "com.soywiz.korlibs.korio:korio:2.2.1"
            }
        }
        nativeMain {

        }
        nativeTest {

        }
    }
}

tasks.create("buildNoExtension") {
    group "build"
    dependsOn "build"
    doLast {
        copy {
            from layout.buildDirectory.dir("bin/native/")
            into layout.buildDirectory.dir("bin/native/")
            rename "timecard.kexe", "timecard"
            duplicatesStrategy DuplicatesStrategy.INCLUDE
        }
        delete("build/bin/native/debugExecutable/timecard.kexe")
        delete("build/bin/native/releaseExecutable/timecard.kexe")
    }
}
